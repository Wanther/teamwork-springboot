buildscript {

    ext['hibernate.version'] = '5.4.2.Final'

    repositories {
        maven { url 'https://maven.aliyun.com/repository/public' }
    }

    dependencies {
        classpath "org.hibernate:hibernate-gradle-plugin:${project['hibernate.version']}"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.4.RELEASE'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.hibernate.orm'

group = 'com.eastalliance'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

bootRun {
    // ./gradlew bootRun --args='--spring.profiles.active=dev'
    main = 'teamwork.App'
}

hibernate {
    enhance {
        enableLazyInitialization = true
        enableDirtyTracking = true
        enableAssociationManagement = true
    }
}

test {
    testLogging.showStandardStreams = true
}

repositories {
    maven { url 'https://maven.aliyun.com/repository/public' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation('org.springframework.boot:spring-boot-starter-undertow') {
        exclude group: 'io.undertow', module: 'undertow-websockets-jsr'
    }
    implementation('org.springframework.boot:spring-boot-starter-cache')

    runtimeOnly 'mysql:mysql-connector-java'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}
